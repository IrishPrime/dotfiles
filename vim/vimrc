syntax on
cd $HOME

if &shell =~# 'fish$'
	set shell=bash
endif

filetype off
set rtp+=~/.vim/bundle/Vundle.vim
call vundle#begin()

" GitHub repos
Plugin 'gmarik/Vundle.vim'
Plugin 'altercation/vim-colors-solarized'
Plugin 'bling/vim-airline'
Plugin 'ctrlpvim/ctrlp.vim'
Plugin 'dag/vim-fish'
Plugin 'gorodinskiy/vim-coloresque'
Plugin 'majutsushi/tagbar'
Plugin 'nathanaelkane/vim-indent-guides'
Plugin 'scrooloose/nerdtree'
Plugin 'scrooloose/syntastic'
Plugin 'tpope/vim-fugitive'
Plugin 'tpope/vim-unimpaired'
Plugin 'xolox/vim-misc'
Plugin 'xolox/vim-notes'
Plugin 'IrishPrime/WhiteWash.vim'
" Vim-Scripts repos
Plugin 'hexHighlight.vim'
Plugin 'matchit.zip'
Plugin 'netrw.vim'

call vundle#end()

set autochdir
set autowrite
set background=dark
set backspace=indent,eol,start
set cursorline
set diffopt+=vertical
set encoding=utf-8
set fillchars+=vert:│,fold:-,diff:-
set foldmethod=indent
set guifont=DejaVu\ Sans\ Mono\ 12
set guioptions-=L
set guioptions-=m
set guioptions-=r
set guioptions-=T
set hidden
set history=50
set ignorecase
set incsearch
set laststatus=2
set linebreak
set listchars=tab:→\ ,eol:¬
set nohlsearch
set number relativenumber
set showcmd
set showmatch
set noshowmode
set smartcase
"set statusline=%F%m%r%h%w\ [%n]%=%y\ [%4l,%3v\ %p%%] " Disable with Airline
set tabstop=4 softtabstop=4 shiftwidth=4 noexpandtab
set tags=tags;/
set t_Co=256
set wildmenu
set wildignore=*.o,*.swp,*.class,*.pyc
set wildmode=list:longest,full
set wrap

" Mouse support, just in case
if has("mouse")
	set mouse=a
endif

if has("autocmd")
	filetype plugin indent on

	if exists("+omnifunc")
		autocmd Filetype *
			\if &omnifunc == "" |
			\	setlocal omnifunc=syntaxcomplete#Complete |
			\endif
	endif

	augroup vimrcEx
		autocmd!
		autocmd FileType vim setlocal textwidth=78
		autocmd BufWritePost .vimrc nested :source $MYVIMRC
		autocmd BufReadPost *
			\if line("'\"") > 1 && line("'\"") <= line("$") |
			\	exe "normal! g`\"" |
			\endif
	augroup END

	augroup Java
		autocmd!
		autocmd BufNewFile *.java  0r $HOME/Templates/Java.java
		autocmd BufNewFile *.java  exe "8,10s/TMP/".expand("%<")
		autocmd FileType java set makeprg=javac\ *.java
	augroup END

	" Automatic spell-checking for *.txt buffers, annoying with help files
	"autocmd BufEnter,BufNew *.txt set spell spelllang=en_us

	autocmd BufNewFile *.c      0r $HOME/Templates/C.c
	autocmd BufNewFile *.php    0r $HOME/Templates/PHP.php
	autocmd BufNewFile *.pl     0r $HOME/Templates/Perl.pl
	autocmd BufNewFile *.py     0r $HOME/Templates/Python.py
	autocmd BufNewFile *.*htm*  0r $HOME/Templates/XHTML.xhtml
	autocmd BufNewFile Makefile 0r $HOME/Templates/Makefile
endif

if has("cscope")
	set cscopetag cscopeverbose

	if has("quickfix")
		set cscopequickfix=s-,c-,d-,i-,t-,e-
	endif

	cnoreabbrev <expr> csa
		\ ((getcmdtype() == ":" && getcmdpos() <= 4)? "cs add"  : "csa")
	cnoreabbrev <expr> csf
		\ ((getcmdtype() == ":" && getcmdpos() <= 4)? "cs find" : "csf")
	cnoreabbrev <expr> csk
		\ ((getcmdtype() == ":" && getcmdpos() <= 4)? "cs kill" : "csk")
	cnoreabbrev <expr> csr
		\ ((getcmdtype() == ":" && getcmdpos() <= 4)? "cs reset" : "csr")
	cnoreabbrev <expr> css
		\ ((getcmdtype() == ":" && getcmdpos() <= 4)? "cs show" : "css")
	cnoreabbrev <expr> csh
		\ ((getcmdtype() == ":" && getcmdpos() <= 4)? "cs help" : "csh")

	"command -nargs=0 CScope cs add $VIMSRC/src/cscope.out $VIMSRC/src

	" Automatically load CScope
	function! LoadCscope()
		let db = findfile("cscope.out", ".;")
		if (!empty(db))
			let path = strpart(db, 0, match(db, "/cscope.out$"))
			set nocscopeverbose " suppress 'duplicate connection' error
			exe "cs add " . db . " " . path
			set cscopeverbose
		endif
	endfunction
	au BufEnter /* call LoadCscope()
endif

function! NeatFoldText()
	let line = ' ' . substitute(getline(v:foldstart), '^\s*"\?\s*\|\s*"\?\s*{{' . '{\d*\s*', '', 'g') . ' '
	let lines_count = v:foldend - v:foldstart + 1
	let lines_count_text = '| ' . printf("%10s", lines_count . ' lines') . ' |'
	let foldchar = matchstr(&fillchars, 'fold:\zs.')
	let foldtextstart = strpart('+' . repeat(foldchar, v:foldlevel*2) . line, 0, (winwidth(0)*2)/3)
	let foldtextend = lines_count_text . repeat(foldchar, 8)
	let foldtextlength = strlen(substitute(foldtextstart . foldtextend, '.', 'x', 'g')) + &foldcolumn
	return foldtextstart . repeat(foldchar, winwidth(0)-foldtextlength) . foldtextend
endfunction
set foldtext=NeatFoldText()

" GetLatestVimScripts options
let g:GetLatestVimScripts_allowautoinstall=1

" Mapping to edit vimrc
nmap <Leader>v :tabedit $MYVIMRC<CR>

" Mappings to navigate my C style
:map [[ ?{<CR>w99[{
:map ][ /}<CR>b99]}
:map ]] j0[[%/{<CR>
:map [] k$][%?}<CR>

" Mappings to make the global register less annoying
if has("clipboard")
	map <Leader>p :set paste<CR>"+]p<Esc>:set nopaste<CR>
	map <Leader>P :set paste<CR>"+]P<Esc>:set nopaste<CR>
	map <Leader>y "+y
	map <Leader>Y "+Y
else
	map <Leader>p :set paste<CR>:r !xsel -ob<Esc>:set nopaste<CR>
	map <Leader>P :set paste<CR>:-1r !xsel -ob<Esc>:set nopaste<CR>
	map <Leader>y :w !xsel -ib<CR><CR>
	map <Leader>Y <S-v>:w !xsel -ib<CR><CR>
endif

" Mappings and commands for dates/times
imap <Leader>ymd <C-R>=strftime("%Y.%m.%d")<CR>
imap <Leader>mdy <C-R>=strftime("%m.%d.%Y")<CR>
imap <Leader>ndy <C-R>=strftime("%b %d, %Y")<CR>
imap <Leader>hms <C-R>=strftime("%T")<CR>
imap <Leader>ynd <C-R>=strftime("%Y %b %d")<CR>
com! YMD :norm! i<C-R>=strftime("%Y.%m.%d")<CR>
com! MDY :norm! i<C-R>=strftime("%m.%d.%Y")<CR>
com! NDY :norm! i<C-R>=strftime("%b %d, %Y")<CR>
com! HMS :norm! i<C-R>=strftime("%T")<CR>
com! YND :norm! i<C-R>=strftime("%Y %b %d")<CR>

" Mapping to open a Quickfix window for the last search.
nnoremap <silent> <Leader>q :execute 'vimgrep /'.@/.'/g %'<CR>:copen<CR>

" Mapping to play recordings on a visual range
nnoremap Q @q
vnoremap Q :norm @q<cr>

" Mapping to toggle the toolbar and menu in gVim
map <silent> <F2> :if &guioptions =~# 'T' <Bar>
	\set guioptions-=T <Bar>
	\set guioptions-=m <Bar>
\else <Bar>
	\set guioptions+=T <Bar>
	\set guioptions+=m <Bar>
\endif<CR>

" Mapping to toggle the NERDTree file browser and NERDTree options
map <F3> :NERDTreeToggle<CR>
let NERDTreeShowBookmarks = 1
let NERDChristmasTree = 1
let NERDTreeHijackNetrw = 1
let NERDTreeIgnore = ['\.pyc$']

" Mapping to toggle Tagbar tag browser
map <F4> :TagbarToggle<CR>

" Mapping to switch between .c and .h files
map <F5> :e %:p:s,.h$,.X123X,:s,.c$,.h,:s,.X123X$,.c,<CR>

" Mapping to toggle spell checking
map <F7> :set spell!<CR>

" Mapping to show syntax item under cursor
map <F9> :echo "hi<" . synIDattr(synID(line("."),col("."),1),"name") . '> trans<'
\ . synIDattr(synID(line("."),col("."),0),"name") . "> lo<"
\ . synIDattr(synIDtrans(synID(line("."),col("."),1)),"name") . ">"<CR>

" Notes.vim options
let g:notes_directories = ['~/Documents/Notes']
let g:notes_suffix = '.txt'
let g:notes_title_sync = 'rename_file'

" Airline options
let g:airline_powerline_fonts = 1
let g:airline_theme = "solarized"
if !exists('g:airline_symbols')
	let g:airline_symbols = {}
endif
let g:airline_symbols.space = "\ua0"
let g:airline_left_sep = ''
let g:airline_left_alt_sep = ''
let g:airline_right_sep = ''
let g:airline_right_alt_sep = ''
let g:airline_symbols.branch = ''
let g:airline_symbols.readonly = ''
let g:airline_symbols.linenr = ''

" Syntastic options
let g:syntastic_error_symbol = '✗'
let g:syntastic_warning_symbol = '»'
let g:syntastic_check_on_wq = 0

" Use The Silver Searcher if available
if executable('ag')
	" Use ag in CtrlP
	let g:ctrlp_user_command = 'ag %s -l --nocolor --hidden -g ""'
	" Skip caching since ag is so fast
	let g:ctrlp_use_caching = 0
endif

let g:solarized_termtrans = 1
color solarized
